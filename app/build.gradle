apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'autoregister.gradle'
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi-v7a' //, 'x86','armeabi', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (!isBuildModule.toBoolean()) {

                //合并到宿主中
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本时.剔除debug文件夹下的所有调式文件
                    exclude 'src/debug/*'
                }
            }
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility rootProject.ext.versions.java
        targetCompatibility rootProject.ext.versions.java
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.androidx["androidx.appcompat"]
    implementation rootProject.ext.androidx["androidx.constraintlayout"]
    testImplementation rootProject.ext.androidx["androidx.junit"]
    androidTestImplementation rootProject.ext.androidx["androidx.test.ext:junit"]
    androidTestImplementation rootProject.ext.androidx["androidx.espresso"]
    kapt rootProject.ext.dependencies["ARouterCompiler"]
    implementation project(":library-network")
    implementation project(":library-service")
    implementation project(":library-res")
    implementation project(":library-base")

    if (isBuildModule.toBoolean()) {
        //当组件独立运行时，需要把基础库依赖到宿主app项目上


    } else {
        //如果是作为组件，需要把各个组件依赖到App宿主上

        // app启动模块
        implementation project(":module-extrance")
        // app首页模块
        implementation project(":module-home")
        // app用户模块
        implementation project(":module-user")
        //wx公众号模块
        implementation project(":module-wx")
        //项目模块
        implementation project(":module-project")

    }
}
